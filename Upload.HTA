<script language="VBScript">
  Sub DoResize
  	Dim intSize : intSize = 640
  	Dim posLeft, posTop
  	
    'resize   
    window.resizeTo intSize, intSize

    posLeft = (Document.ParentWindow.Screen.AvailWidth - intSize) / 2
    posTop = (Document.ParentWindow.Screen.AvailHeight - intSize) / 2     
    'move to centerscreen
    window.moveTo posLeft, posTop
  End sub

  'Run the subroutine to position the window before the anything is rendered.
  DoResize() 
</script>

<head>
<title>eDiscovery EDMS Uploader v2.3B</title>
<HTA:APPLICATION ID="appShareHTA"
     APPLICATIONNAME="eDiscovery Uploader"
     SCROLL="no"
     SINGLEINSTANCE="yes"
     WINDOWSTATE="Normal"
     VERSION="1.3"
/>
<meta name="description" content="Case File Archive Uploader">
<meta name="last modified" content="04/12/2021">
<meta name="author" content="Eric S. Pooch">
<style>
	body{font-family:Calibri;}
	h5{margin-bottom:0px;}
</style>
</head>
<body onhelp="ShowHelp">
<span id = "Processing"></span>
<script language = "VBScript">

Const debugLvl = 5: Const infoLvl = 4 : Const warnLvl = 3: Const errorLvl = 2 : Const fatalLvl = 1 : Const silentLvl = 0
Dim logLvl : logLvl = infoLvl

Dim objFSO   : Set objFSO = CreateObject("Scripting.FileSystemObject")
Dim objShell : Set objShell = CreateObject("WScript.Shell")

Const archRoot = "CaseFile_Archive"
Const archPath  = "\\ad\dfs\sdat-shared2\Attorney\CRIMCASE\CaseFile_Archive\"
Dim aryArchPaths : aryArchPaths = Array(archPath, "L:\CaseFile_Archive\")

Dim clientPath : clientPath = archPath & "Resources\05_Client\"

Dim scanPath : scanPath  = "\\ad\dfs\SDAT-Shared2\Attorney\CRIMCASE\CRIMFORM\eDiscovery_EDMS\eDisCityAtty\"
'Send to Testing folder for first round of testing
'Dim scanPath : scanPath  = "\\ad\dfs\SDAT-Shared2\Attorney\CRIMCASE\CRIMFORM\eDiscovery_EDMS_Test\eDisCityAttyTest"
Const cloudPath  = "\\ad\dfs\SDAT-Shared2\Attorney\CRIMCASE\CRIMFORM\eDiscovery_EDMS\FileCloud\"
Dim eSharePath : eSharePath = "\\ad\dfs\SDAT-Shared2\Attorney\CRIMCASE\CRIMFORM\eDiscovery_EDMS\eShare\"

Const strGSPath	= "C:\Program Files\gs\gs9.53.3\bin\gswin64c.exe"
Dim strGSExePath : strGSExePath = strGSPath & "gswin64c.exe"

' Make sure we have network connection
If NOT (objFSO.FolderExists(scanPath) AND objFSO.FolderExists(archPath) ) Then
	ShowMessage errorLvl, "Scanner Folder Not Found " & vbCRLF & scanPath & vbCRLF &_
		"Try Restarting computer to restore network connection."
	Self.Close()
End If

' Pull out the file path argument from the command string.
Dim strCommand  : strCommand  = appShareHTA.commandLine 
Dim strFilePath : strFilePath = Trim(Split(strCommand, "/f:")(1))

' Pull out the enable remote discovery and debug options from the command string.
Dim boolNoDisco   : boolNoDisco   = InStr(strCommand, " /disco ") <=0
Dim boolDebug : boolDebug = InStr(strCommand, " /debug ") > 0
If boolDebug Then
	scanPath = "\\ad\dfs\sdat-shared2\Attorney\CRIMCASE\CRIMFORM\eDiscovery_EDMS_Test\eDisCityAttyTest\"
	eSharePath = "\\ad\dfs\SDAT-Shared2\Attorney\CRIMCASE\CRIMFORM\eDiscovery_EDMS_Test\eShare\"
End If

Dim aryFilePaths : aryFilePaths = getPathsFromArgs(strFilePath)
strFilePath = aryFilePaths(0)

Dim strFileName : strFileName = objFSO.GetFileName(strFilePath)
Dim strCMSCase  : strCMSCase = Split(strFileName, "_")(0)

'Determine if the file is already in the archive.
'If so, get the case number from the folder name instead of the file.
Dim boolInArch : boolInArch = False
Dim tmpPath
For Each tmpPath in aryArchPaths
	If InStr(strFilePath, tmpPath) = 1 Then
		boolInArch = True
		strCMSCase = Split( Replace(strFilePath, tmpPath, ""), "\")(0)
	End If
Next


Dim aryImageTypes : aryImageTypes = Array("jpg", "jpeg", "png", "gif", "pict", "raw", "tif", "tiff")
Dim boolPics : boolPics =  	UBound(aryFilePaths) > 0
Dim boolGS : boolGS =  		objFSO.FolderExists(strGSPath)
	
Dim boolPro : boolPro = ReadRegistry("HKEY_CURRENT_USER\Software\Adobe\Adobe Acrobat\DC\AVEntitlement\iEntitlementLevel")
If boolPro = 300 Then
	boolPro = True
Else
	boolPro = False
End If
 
If boolPics Then
	boolPics = False
	Dim strImageTypes
	For Each strImageTypes In aryImageTypes
		If LCase(objFSO.GetExtensionName(strFileName)) = strImageTypes Then
		 	boolPics = True
		End If
	Next
End If

If boolDebug Then
	logLvl = debugLvl
	ShowMessage debugLvl, "Debug mode enabled" & vbCrLf & strCommand
	ShowMessage debugLvl, Join(aryFilePaths, vbCrLf)
	ShowMessage debugLvl, "boolPro:" & boolPro
    ShowMessage debugLvl, "boolInArch:" & boolInArch & vbCrLf & objFSO.GetParentFolderName(strFilePath)
ElseIf False Then ' set to true to warn users.
	ShowMessage fatalLvl, "Undergoing Maintenance. Try again later."
End If

Dim strFileHelp : strFileHelp = TypeErrorForFile(strFileName)

If NOT (objFSO.FileExists(strFilePath) OR objFSO.FolderExists(strFilePath)) Then
	strFileHelp = "The file could not be found. Try again."
End If

If Len(strFileHelp) Then
	ShowMessage fatalLvl, strFileName & " is not a valid data file. " & strFileHelp
End If

Sub Window_OnLoad
  	'This method will be called when the application loads
    SetFormContent()
End Sub

Sub Upload()
	Dim strCopyPath
	
	If Len(CMSNumber.Value) < 6 Or InStr(CMSNumber.Value, "M") = 1 Then
		ShowMessage errorLvl, "You must enter a valid CMS Case Number"
		Exit Sub
	End If
	
	Processing.InnerHTML = "Processing... "
	SubmitButton.InnerHTML = ""
	
	If coversheet.Checked Then
		'Add a coversheet to the list of files
		ReDim Preserve aryFilePaths(UBound(aryFilePaths) + 1)
		aryFilePaths(UBound(aryFilePaths)) = SaveCoverSheet()
	End If

	For Each objButton in office
		If objButton.Checked Then
		
			If objButton.value = "Archive" Then
				strCopyPath = scanPath
				objShell.Run "explorer.exe /e," & archPath & CMSNumber.Value
				
			ElseIf objButton.value = "FileCloud" Then
				strCopyPath = cloudPath
				objShell.Run "https://filecloud.sandiego.gov/"
				
			ElseIf objButton.value = "Retained" Then
				'Prepare an email with the files as attachments, then save the sent email as proof.
				Processing.InnerHTML = "Waiting for Outlook... "
				Call UploadNextSentEmail( PrepEmail(aryFilePaths) )
				Processing.InnerHTML = "Processing... "
			Else
				If NOT objFSO.FolderExists(archPath & CMSNumber.Value) Then
					ShowMessage errorLvl, "This discovery cannot be sent to eShare without an Initial Disclosure."
     				Exit Sub
				Else
					strCopyPath = eSharePath & objButton.value & "\"
					objShell.Run "explorer.exe /e," & archPath & CMSNumber.Value
				End If
			End If
			Exit For
  		End if
	Next

	If placeholder.Checked Then 
		PrintPlaceHolder()
	End If

	If IsEmpty(strCopyPath) Then
 		Self.Close()
	End If

	If pdfsummary.Checked Then
		If boolPics Then
			'Generate the contact sheet
			If boolGS Then
				strFilePath = ContactSheetGS(aryFilePaths)
			ElseIf boolPro Then
				strFilePath = ContactSheet(aryFilePaths)
			End If
			If Len(strFilePath) Then
				'Send the contact sheet to the recipient 
				objFSO.MoveFile strFilePath, _
					objFSO.BuildPath(strCopyPath, CMSNumber.Value & "_PHT_" & "Photo-Summary."& objFSO.GetExtensionName(strFilePath))
			
				'Send the original photos just to the archive
				If Not boolInArch Then
					strCopyPath = scanPath
				Else
					Self.Close()
				End If
			Else
				ShowMessage errorLvl, "Cannot convert pictures to PDF"
			End If
		Else
			ShowMessage errorLvl, "These are not Pictures."
		End If
	End If
	
	For Each strFilePath In aryFilePaths
		Dim boolError : boolError = False
		Dim strFileHelp : strFileHelp = TypeErrorForFile(strFilePath)
		
		If Len(strFileHelp) > 0 Then
			boolError = True
			ShowMessage errorLvl, strFileHelp
		ElseIf objFSO.FolderExists(strFilePath) Then
			'zip it
			strFilePath = CompressFolder(strFilePath)
			
		ElseIf objFSO.GetFile(strFilePath).Size > 740000000 Then
			boolError = True
			ShowMessage errorLvl, "This file is too big to proceess:" & vbCrLf & strFilePath
		End If
		
		If boolError Then
			'skip processing 
		ElseIf objFSO.FileExists(strFilePath) Then
			if boolDebug Then MsgBox(objFSO.BuildPath(strCopyPath, FormatFileName(objFSO.GetFileName(strFilePath))) )

			objFSO.CopyFile strFilePath, objFSO.BuildPath(strCopyPath, FormatFileName(objFSO.GetFileName(strFilePath)))
			
		Else
			ShowMessage errorLvl, "File Not Found " & vbCRLF & strFilePath
		End If
	Next

    Self.Close()
End Sub

Function FormatFileName(strFileName)
	Dim strDestName : strDestName = strFileName
	
	Dim strDefIdx : strDefIdx = Split(Defendant.Value,".")(0)
	if strDefIdx <> "" Then strDefIdx = "." + strDefIdx
	Dim strPrefix : strPrefix = CMSNumber.Value & strDefIdx & "_"

	' Add the defendant index, if specified
	strDestName = Replace(strDestName, (CMSNumber.Value & "_"), strPrefix, 1, 1)

	If strCMSCase <> CMSNumber.Value OR FileType.Value <> "" OR InStr(strFileName, (CMSNumber.Value & "_")) <> 1 Then
   		' Get rid of '_'s that will confuse the server.
		strDestName = Replace(strDestName, "_", "-")

		If FileType.Value <> "" Then
			' Add the destination folder for the file type
			strDestName = FileType.Value & "_" & strDestName
		End If
		'"<CMSNUMBER> "
		strDestName = Replace(strDestName, (CMSNumber.Value & " "), "", 1, 1)
		strDestName = Replace(strDestName, (CMSNumber.Value & "-"), "", 1, 1)

		'"<CMSNUMBER>_"
		strDestName = strPrefix & strDestName
	End If
	FormatFileName = strDestName
End Function

'*********************************************************
' Purpose:	Prepare (but do not send) an Outlook email with the specified attachements.
' Inputs:	aryFilePaths: array of file paths to the attachments.
'*********************************************************
Function PrepEmail(aryFilePaths)
	Const olFolderSentMail = 5

	On Error Resume Next
	Dim objOutlook: Set objOutlook = GetObject(, "Outlook.Application")
	If Err.Number <> 0 Then
		' Outlook is not running
		objShell.Run "OUTLOOK.EXE"
		Set objOutlook = CreateObject("Outlook.Application")

	End If
	Err.Clear

	Dim strFilePath
	Dim strCaseNum : strCaseNum = CMSNumber.Value
	Dim strSubject : strSubject = "[Encrypt_]+" & Left(objFSO.GetBaseName(aryFilePaths(0)), 40) & "+ New CA Discovery"
	Dim strBody : strBody = "Attention: New Discovery attached for City Attorney Case " & strCaseNum & "." & vbCrLf &_
						formatDefendantInfo() & vbCrLf &_ 
						"You may also pick up a copy at this office, allowing for approximately 5-10 minutes for us to process your request. " &_ 
						"Bring this notice with you for faster service." & vbCrLf &_
						"$0.25 - Page Photocopy; $7.45 - CD or DVD" & vbCrLf &_
						"$8.55 - 64GB Flash Drive; $14.00 - 128GB Flash Drive" & vbCrLf & vbCrLf &_
						"Office of the City Attorney" & vbCrLf &_
						"1200 3rd Ave #700" & vbCrLf &_
						"San Diego, CA 92101" & vbCrLf &_
						"619-533-5500"  & vbCrLf & vbCrLf &_
						"For CA office reference:" & vbCrLf & formatFileList()

	Set objMail = objOutlook.CreateItem(0)
	With objMail
		.Subject = strSubject
		.Body = strBody
		For Each strFilePath In aryFilePaths
			If objFSO.GetFile(strFilePath).Size > 20000000 Then
				' Otherwise Outlook will not show an error and just not attach the file.
				ShowMessage warnLvl, "The file you're attaching may be bigger than the server allows. Try attaching parts of the file to multiple emails, or uploading the file to a shared location." & vbCrLf & vbCrLf & strFilePath
			End If
			'objShell.Run "OUTLOOK.EXE /c ipm.note /m ""?subject=" & strSubject &_
				'		"&body=" & strBody & strFilePath &_
				'		""" /a " & """" & strFilePath & """"
			.Attachments.Add(strFilePath)
		Next

		PrepEmail = objOutlook.GetNamespace("MAPI").GetDefaultFolder(olFolderSentMail).Items.Count
		
		.Display(true) ' true make it modal. Script will wait here until window closes.
	End With
End Function

'*********************************************************
' Purpose:	Wait until a new email shows up in the user's Outlook Sent Items.
'			Verify that the user wants to save it, and store in Archive.
'			This is the proof that the user emailed the discovery from PrepEmail().
' Inputs:	intSentCount: Original number of emails in sent Items. 
'			If set to 0, it will be calculated here.
'			Generally needs to be calculated before this function as there is a 
'			race condition (for sent items >10K) between calculating the initial 
'			number of items in the sent mail and PrepEmail() sending the email 
'			from modal outlook display.
'*********************************************************
Sub UploadNextSentEmail(intSentCount)
	Const olFolderSentMail = 5
	Dim objOutlook : Set objOutlook = CreateObject("Outlook.Application")

	Dim objSentFldr : Set objSentFldr = objOutlook.GetNamespace("MAPI").GetDefaultFolder(olFolderSentMail)
	If intSentCount = 0 Then
		intSentCount = objSentFldr.Items.Count
	End If
	
	Dim dateStart: dateStart = Now 	' Start of 20 sec timeout

	Do Until objSentFldr.Items.Count > intSentCount
		If DateDiff("s", dateStart, Now) > 20 Then
			Dim result : result = MsgBox("Unable to find Email in your Sent Items. You may have closed the message without sending it. Keep looking?", vbRetryCancel, Document.title)
			If result = vbRetry Then
				' Reset the timeout
				dateStart = Now
			Else 
				Exit Sub
			End If
		End If
	Loop

	Dim colSent : Set colSent = objSentFldr.Items '.Restrict("[Unread]=true")
	'Set colSent = objFolder.Items.Restrict("[Subject] = " & strSubject)
	colSent.Sort "[ReceivedTime]"

	Dim objSentMsg: Set objSentMsg = colSent.Item(colSent.Count)
	Dim res
	res = MsgBox("Save sent Email message to " & objFSO.GetFileName(archPath) & "?" & vbCrLF &_
				"To: " & objSentMsg.To & vbCrLF &_
				"Subject: " & vbCrLF &_
				 objSentMsg.Subject, vbYesNo OR vbQuestion, Document.title)
	If res = vbYes Then
		' Get rid of text before and after "+" in the subject to simplify the file name.
		Dim aryNames : aryNames = Split(Split(objSentMsg.Subject & "+", "+")(1), "_")
		' Insert the PAS (Private Attorney Share) file type.
		aryNames(0)=aryNames(0) & "_PAS"
		'Server assumes 2nd name component is the file name. HACK:
		Dim strNamePart : strNamePart = aryNames(1)
		If Len(strNamePart) = 3 AND strNamePart = UCase(strNamePart) Then
			'Probably a type. This will confuse the server and we lose the description.
			aryNames(1) = "_"
		End If
		strNamePart = Join(aryNames, "_")
		strNamePart = Replace(strNamePart, "___", "_")
		' Rebuild the file name for the server to parse.
		objSentMsg.SaveAs objFSO.BuildPath(scanPath, strNamePart) & ".msg"
	End If
End Sub

'*********************************************************
' Purpose:	Turn the script arguments into an array of paths.
' 			If there are spaces in a path, it will be quoted.
' 			If there are not spaces, it will not be quoted.
' 			This makes it difficult to determine how to separate the paths 
' 			when there are multiple paths, some quoted, some not.
' Inputs:	strPathList: string with a bunch of quoted or unquoted paths.
' Returns:	Array of paths to the specified files.
'*********************************************************
Function getPathsFromArgs(strPathList)

	Dim boolInQuote	: boolInQuote = False
	Dim numStartLoc : numStartLoc = 1
	Dim numEndLoc   : numEndLoc = 1
	Dim numEndFinal : numEndFinal = Len(strPathList)
	
	While numEndLoc < numEndFinal
		Dim numLength
		numEndLoc = InStr(numStartLoc, strPathList, """")
		If numEndLoc <= 0 Then numEndLoc = numEndFinal + 1
		
		numLength = (numEndLoc - numStartLoc)
		
		If boolInQuote Then
			strPaths = strPaths & Mid( strPathList, numStartLoc, numLength) & vbCrLf
			numEndLoc = numEndLoc + 1
		ElseIf numLength > 1 Then
			strPaths = strPaths & Replace( Trim( Mid( strPathList, numStartLoc, numLength)), " ", vbCrLf ) & vbCrLf
		End If
		numStartLoc = numEndLoc + 1
					
		boolInQuote = Not boolInQuote
		
		'WScript.Echo(strPaths & numStartLoc & " " & numEndLoc & " " & numEndFinal)
	Wend
	strPaths = Left(strPaths, Len(strPaths) - Len(vbCrLf))
	getPathsFromArgs = Split(strPaths, vbCrLf)
End Function


'*********************************************************
' Purpose:	Get the saved defendants for the case
'*********************************************************
Function getDefendants()
	Dim strCasePath, objFile, strDefs

	strDefs = ","
	strCasePath = Left(strFilePath, InStr(strFilePath, strCMSCase) + Len(strCMSCase))
	' This is not guaranteed to be alpha order, but it always appears to be.
	Set getDefendants = objFSO.GetFolder(strCasePath).Files
End Function

'*********************************************************
' Purpose:	read the saved info for sepcified defendant for the case
' Returns:	dictionary of info for defendant
'*********************************************************
Function getDefendantInfo(strDefFile)
	Dim dctInfo : Set dctInfo = CreateObject("Scripting.Dictionary")

	If boolDebug Then MsgBox strDefFile

	Const ForReading = 1
	Dim strCasePath, strlines
	strCasePath = Left(strFilePath, InStr(strFilePath, strCMSCase) + Len(strCMSCase))

	Set objText = objFSO.OpenTextFile (objFSO.BuildPath(strCasePath, strDefFile), ForReading)

	Do Until objText.AtEndOfStream
		aryKeyVal = Split(objText.Readline(), " = ")
		if UBound(aryKeyVal) > 0 Then
			dctInfo.Add aryKeyVal(0), aryKeyVal(1)
		End If
	Loop
	objText.Close

	Set getDefendantInfo = dctInfo
End Function

'*********************************************************
' Purpose:	Consolidate multiple image files into one PDF
'			with file name caption at a reduced resolution
'			Using ghostScript.
' Inputs:	aryFiles: array of JPEG or GIF file paths.
' Returns:	Path to the created file, in the %TEMP% folder.
'*********************************************************
Function ContactSheetGS(aryFiles)
	Dim strViewList : strViewList = ""
	ContactSheetGS = objFSO.BuildPath(objShell.ExpandEnvironmentStrings( "%TEMP%" ), objFSO.GetTempName & ".pdf")
	'Run ghostscript quiet, with pdf output, ebook setting:150dpi, output to the temp file,
	'	caption of the first file name to set the caption box size,
	'	include modified caption.ps for caption code, 
	'	include standard viewjpeg.pd and viewjpeg.ps for view image code.
	Dim strGSCommand : strGSCommand = """" & strGSExePath & """" &_ 
		" -dQUIET -dBATCH -dNOPAUSE -sDEVICE=pdfwrite -dPDFSETTINGS=/ebook" &_
		" -sOutputFile=""" & ContactSheetGS & """" &_
		" -sCap=""" & objFSO.GetFileName(aryFiles(0)) & """" &_
		" """ & clientPath & "caption.ps" & """ """ & strGSPath & "viewjpeg.ps" & """ """ &  strGSPath & "viewjpeg.ps"""
		
	For Each strPicPath In aryFiles
		' Each file gets a flag "-sCap=" to set the cation text for modified caption.ps,
		' and a call to viewJPEG (or viewGIF) to display the image, and a showpage to complete the page.  
		' -sCap="IMG_245.jpg" -c "(D:/-/-/IMG_245.jpg) viewJPEG showpage"
		If LCase(objFSO.GetExtensionName(strPicPath)) = "jpg" Then
			strViewList = strViewList & " -sCap=""" & objFSO.GetFileName(strPicPath) & """" & " -c ""(" & strPicPath & ") viewJPEG showpage"""
			
		ElseIf LCase(objFSO.GetExtensionName(strPicPath)) = "gif" Then
			strViewList = strViewList & " -sCap=""" & objFSO.GetFileName(strPicPath) & """" & " -c ""(" & strPicPath & ") viewGIF  showpage"""
		End If
	Next
	'Apparent path separator bug in viewJPEG
	strViewList = Replace(strViewList, "\", "/")

	strGSCommand = strGSCommand & strViewList
	shellExec(strGSCommand)
	
	If Not objFSO.FileExists(ContactSheetGS) Then
		 ContactSheetGS =  ""
	End If
End Function


'*********************************************************
' Purpose:	Consolidate multiple image files into one PDF
'			with file name caption at a reduced resolution
'			Using Acrobat and WIA.
' Inputs:	aryFiles: array of JPEG or GIF file paths.
' Returns:	Path to the created file, in the %TEMP% folder.
'*********************************************************
Function ContactSheet(aryFiles)
	Dim AcroApp :	Set AcroApp = CreateObject("AcroExch.App")
	Dim PDDoc : Set PDDoc = CreateObject("AcroExch.PDDoc")
	Dim strPicPath, strPicExt, blnRet
	
	Dim strPDFPath : strPDFPath = objFSO.BuildPath(objShell.ExpandEnvironmentStrings( "%TEMP%" ), objFSO.GetTempName & ".pdf")
	ContactSheet = strPDFPath

	'Create the summary PDF.
	If PDDoc.Create <> True Then
		 ShowMessage errorLvl, "Unable to create new PDF file. The Acrobat application may not support editing."
		 AcroApp.Exit()
		Exit Function
	End If
	
	For Each strPicPath In aryFiles
		Dim strPicName : strPicName = objFSO.GetFileName(strPicPath)
	 	Dim strFileExt : strFileExt = LCase(objFSO.GetExtensionName(strPicPath))
	 	
	 	Dim i, boolIsImage : boolIsImage = False
	 	For i = 0 to UBound(aryImageTypes)
	 		If strFileExt = aryImageTypes(i) Then
	 			boolIsImage = True
	 			i = 100
	 		End If
	 	Next

		If boolIsImage Then
					
			Dim tmpPicPath : tmpPicPath = OptimizeImageToSize(strPicPath, "pcscreen", 10)
			If AddImageCapPage(PDDoc, tmpPicPath, strPicName) <> True Then
				ShowMessage errorLvl, "Failed to add file to PDF: " & tmpPicPath
			End If
			
			If PDDoc.Save(1 Or 32, strPDFPath) <> True Then
				ShowMessage errorLvl, "Failed to save file: " & strPDFPath
			End If
			
			If tmpPicPath <> strPicPath Then
				objFSO.GetFile(tmpPicPath).Delete
			End If
		End If
	Next
	
	' Fill in pdf properties.
	PDDoc.SetInfo "Title", "Low Res Photograph Summary Sheet"
	PDDoc.SetInfo "Author", "San Diego City Attorney's Office"
	PDDoc.SetInfo "Subject", "CA case number: " & CMSNumber.Value
	PDDoc.SetInfo "Keywords", "%CA$13$PHT%,CA%" & CMSNumber.Value
	
	If PDDoc.Save(1 Or 32, strPDFPath) <> -1 Then
				ShowMessage errorLvl, "Failed to save " & strPDFPath
	End If
		
	PDDoc.Close()
	'AcroApp.CloseAllDocs()
	AcroApp.Exit()
End Function

'*********************************************************
' Purpose:	Add an image to the PDDoc, with specified caption. 
' Inputs:	PDDoc: "AcroExch.PDDoc" object.
'			strPicPath: Path to the image to add.
'			strPicName: Caption to add.
'Returns:	True (-1) if successful. False (0) if error.
'*********************************************************
Function AddImageCapPage(PDDoc, strPicPath, strPicName)
	Dim AVDoc : Set AVDoc = CreateObject("AcroExch.AVDoc")
	Dim blnRet : blnRet = AVDoc.Open(strPicPath, strPicName)
	AddImageCapPage = False
	
	If blnRet <> True Or Not AVDoc.IsValid Then
		ShowMessage errorLvl, "Unable to open file: " & tmpPicPath
	Else
		Dim PDTmpDoc : Set PDTmpDoc = AVDoc.GetPDDoc()
		
		'Insert picture page into the pdf Doc
		blnRet = PDDoc.InsertPages(PDDoc.GetNumPages-1, PDTmpDoc, 0, PDTmpDoc.GetNumPages, False)
		If blnRet <> True Then
			ShowMessage errorLvl, "Could not insert image: " & strPicPath
		Else
			AddCaption PDDoc, PDDoc.GetNumPages-1, strPicName
			AddImageCapPage = True
		End If
				
		PDTmpDoc.Close()
	End If
	
	AVDoc.Close(1)

End Function
'*********************************************************
' Purpose:	Interface to ReSizeImage with easy inputs and DPI calculation.
' Inputs:	strPath: Path to the image file.
' 			strOpt: Acrobat Distiller optimization pre-set 
'					(screen, ebook, printer, prepress).
'			intMaxDimInch : Maximum dimension in inches.
' Returns:	ReSizeImage result : path to the created file, in the %TEMP% folder.
'*********************************************************
Function OptimizeImageToSize(strPath, strOpt, intMaxDimInch)
	Dim intRes

	Select Case LCase(strOpt)
		Case "screen"
			intRes = 72
		Case "pcscreen"
			intRes = 96
		Case "ebook"
			intRes = 150
		Case "printer"
			intRes = 300
		Case "prepress"
			intRes = 300
		Case Else '"default"
			intRes = 72
	End Select

	OptimizeImageToSize = ReSizeImage(strPath, intRes, intMaxDimInch * intRes)
End Function

'*********************************************************
' Purpose:	ReSize an image, including adjusting DPI, so that
'			images with same settings will fill PDF page the same way. 
' Inputs:	strPath: Path to the image file.
' 			intRes: desired resolution DPI.
'			intMaxDim: Maximum dimension in pixels.
' Returns:	Path to the created file, in the %TEMP% folder.
'*********************************************************
Function ReSizeImage(strPath, intRes, intMaxDim)
	Dim wiaImageFile : Set wiaImageFile = CreateObject("WIA.ImageFile")
	Dim wiaImageProcess : Set wiaImageProcess = CreateObject("WIA.ImageProcess")
	ReSizeImage = strPath
				
	wiaImageProcess.Filters.Add wiaImageProcess.FilterInfos("Exif").FilterID
	wiaImageProcess.Filters.Add wiaImageProcess.FilterInfos("Exif").FilterID	
	wiaImageProcess.Filters.Add wiaImageProcess.FilterInfos("Scale").FilterID
	ShowMessage debugLvl, "Resize " & strPath
	If wiaImageFile.LoadFile(strPath) = False Then 
		If wiaImageFile.Width > intMaxDim Or wiaImageFile.Height > intMaxDim Then
			Dim intNewWidth, intNewHeight
			
			ReSizeImage = objFSO.BuildPath(objShell.ExpandEnvironmentStrings( "%TEMP%" ),_
										  objFSO.GetTempName & "." & objFSO.GetExtensionName(strPath) )
										  
			If wiaImageFile.Width > wiaImageFile.Height Then
				intNewWidth = intMaxDim
				intNewHeight = Round( (wiaImageFile.Height/wiaImageFile.Width) * intMaxDim)
			Else
				intNewHeight = intMaxDim
				intNewWidth = Round( (wiaImageFile.Width/wiaImageFile.Height) * intMaxDim)
			End If

			'ShowMessage debugLvl, "Resize " & strPath & vbCRLF &_
			'	"From: " & wiaImageFile.Height & " x " & wiaImageFile.Width & " @ " & wiaImageFile.HorizontalResolution & " DPI" & vbCrLf &_
			'	"To  : " & intNewHeight & " x " & intNewWidth & " @ " & intRes & " DPI"

			'vertical resolution
			wiaImageProcess.Filters(1).Properties("ID") = 282
			wiaImageProcess.Filters(1).Properties("Value") = intRes
			
			'horizontal resolution			
			wiaImageProcess.Filters(2).Properties("ID") = 283
			wiaImageProcess.Filters(2).Properties("Value") = intRes
						
			wiaImageProcess.Filters(3).Properties("MaximumWidth") = intNewWidth
			wiaImageProcess.Filters(3).Properties("MaximumHeight") = intNewHeight
			
			
			Set wiaImageFile = wiaImageProcess.Apply(wiaImageFile)
			
			If wiaImageFile.SaveFile(ReSizeImage) <> False Then
				ShowMessage errorLvl, "Unable to save image " & ReSizeImage 
				ReSizeImage = strPath
			End If
		End If
	Else
		ShowMessage errorLvl, "Unable to load image " & strPath 
	End If
End Function


Sub AddCaption(PDDoc, intPage, strCaption)
	Dim objJSO : Set objJSO = PDDoc.GetJSObject()
	' Calculate the page width
	Dim aRect : aRect = objJSO.getPageBox("Media", intPage)
	Dim intWid : intWid = aRect(2) - aRect(0)

	'Add the caption box
	Dim objTitleBox
	Set objTitleBox = objJSO.AddField("Caption_" & intPage, "text", intPage, Array(Round(intWid/3), 40, Round((intWid/3)*2), 10))
	'Set the caption text
	objTitleBox.Value = strCaption
	objTitleBox.Alignment = "center"
	objTitleBox.TextFont = "Courier"
	objTitleBox.TextSize = 12
	objTitleBox.fillColor = Array("G", .6)
	'Flatten the field into a text box.
	objJSO.flattenPages()
End Sub

'*********************************************************
' Purpose:	Run a shell command and return the results.
' Inputs:	command: the command to run.
' Returns:	output from the command or the return code.
'*********************************************************
Function shellExec(command)
	Dim exec: Set exec =  objShell.Exec(command)
	Dim tmpOutput	:	tmpOutput = ""
	Dim intLoopCount:	intLoopCount = 0

	ShowMessage infoLvl, "exec command: "  & vbCrLf & command

	Do 
		tmpOutput = tmpOutput & exec.StdOut.ReadAll()
		tmpOutput = tmpOutput & vbCrLf & Exec.StdErr.ReadAll()
	Loop Until exec.Status OR intLoopCount > 30000
	
	If Len(tmpOutput) > 0 Then
		ShowMessage infoLvl, "Exec output: " & vbCrLf & tmpOutput
		shellExec = tmpOutput
	End If
End Function


'*********************************************************
' Purpose:	Return formatted defendant info string from Defendant.Value pulldown
'*********************************************************
Function formatDefendantInfo()
	If boolDebug Then MsgBox(Defendant.Value)

	' Remove the defendant index from the value to get just the filename.
	strDefFile = Mid(Defendant.Value, Instr(Defendant.Value,".")+1)
	
	If Len(strDefFile) < Len("_.txt") Then
		formatDefendantInfo = ""
		Exit Function
	End If
	
	Set dctDefInfo = getDefendantInfo(strDefFile)

	' Format the dictionary into a usable string and return it.
	formatDefendantInfo = "Defendant: " + dctDefInfo("LN") + ", " + dctDefInfo("FN") + "	DOB:" + dctDefInfo("DO") + vbCRLF _
				+ "CT Number: " + dctDefInfo("CT") + "		CA Number: " + dctDefInfo("CA") + vbCRLF
End Function

'*********************************************************
' Purpose:	Return formatted list of file paths for use in emails and cover sheets.
'*********************************************************
Function formatFileList()
	Dim strFileList, strfile : strFileList = ""
	For Each strfile in aryFilePaths
		' cut off the leading path elements
		strRelPath = Mid( strFile, InStr(strFile, archRoot) + Len(archRoot) )
		strFileList = strFileList + strRelPath + vbCRLF
	Next
	formatFileList = strFileList
End Function

'*********************************************************
' Purpose:	Prepare and save a cover sheet 
'			with information about the case file
'*********************************************************
Function SaveCoverSheet
	Const strCover = "CoverSheet"

	strFileList = ""
	For Each strfile in aryFilePaths
		strFileList = strFileList + objFSO.GetFileName(strFile)	+ vbCRLF
	Next
	
	strDefInfo = formatDefendantInfo()
	If Len(strDefInfo) = 0 Then
		MsgBox "Can't add Defendant information to " & strCover & ". No valid Defendants selected."
	End If

	strTextInfo = strDefInfo + "Attached File(s):" + vbCRLF + formatFileList()

	Set objWord = CreateObject("Word.Application")
	objWord.Visible = True
	//MsgBox(clientPath & strCover & ".dotx")
	Set objDoc = objWord.Documents.Add(clientPath & strCover & ".dotx")
	
	Set objRange = objDoc.Bookmarks("CT_NUM2").Range
	objRange.Text = strTextInfo
	strFileBaseName = Split(Defendant.Value, ".")(1)
	strDestPath = objFSO.BuildPath (objFSO.GetSpecialFolder(2), (CMSNumber.Value & "_COR_" & strFileBaseName  & ".docx" ))

	objDoc.SaveAs strDestPath
	objDoc.Close(0) ' Don't Save
	'objWord.Quit()
	If boolDebug Then MsgBox strDestPath
	SaveCoverSheet = strDestPath
End Function


'*********************************************************
' Purpose:	Prepare and print a place holder sheet 
'			with paths to the media files to go into the case file.
'*********************************************************
Sub PrintPlaceHolder
	Set objWord = CreateObject("Word.Application")
	objWord.Visible = True
	Set objDoc = objWord.Documents.Add(clientPath & "Digital Media Place Holder.dotx")
	
	Set objSelection = objWord.Selection
	objSelection.EndKey 6, 0
	
	objSelection.TypeParagraph()
	objSelection.TypeText Join(aryFilePaths,vbCRLF)
	
	objDoc.PrintOut()
	objDoc.Close(0) ' Don't Save
	objWord.Quit()
End Sub

'*********************************************************
' Purpose:	Try to use the right preview for the type of document.
'*********************************************************
Sub EmbedPreview
    window.resizeTo 640,820
	If objFSO.FolderExists(strFilePath) Then
		Preview.InnerHTML = strFilePath & "<br><br>Directory contents will be compressed before sharing or archiving."
		Exit Sub
	ElseIf UBound(aryFilePaths) > 0 Then
		Preview.InnerHTML = strFilePath & "<br><br>All selected files will be sent with the same CMS information."
	End If

	Preview.InnerHTML = strFilePath & "<br><iframe src='" & strFilePath & "' width='480' height='290'></iframe>"
End Sub

Function CompressFolder(strFolderPath)
    Dim zipFile 
    Dim intLoopCount : intLoopCount = 20000
    
    With objFSO
		Dim i : i = 2
		zipFile = .BuildPath(_
			objShell.ExpandEnvironmentStrings( "%TEMP%" ),_
			.GetBaseName(strFolderPath) & ".zip")
		
		While .FileExists(zipFile)
			zipFile = .BuildPath( _
				.GetParentFolderName(zipFile),_
				.GetBaseName(strFolderPath) & " (" & i & ").zip")
			i = i + 1
		WEnd
	End With
	
    With CreateObject("Scripting.FileSystemObject")

        With .CreateTextFile(zipFile, True)
            .Write Chr(80) & Chr(75) & Chr(5) & Chr(6) & String(18, chr(0))
	    	.Close
        End With
    End With

    With CreateObject("Shell.Application")
		Dim zipDir : Set zipDir = .NameSpace(zipFile)
		Dim folderDirItems : Set folderDirItems = .NameSpace(strFolderPath).Items
	
	        zipDir.CopyHere(folderDirItems)
	
	 	While ( zipDir.Items.Count < folderDirItems.Count) 'AND intLoopCount > 0
	
		'	intLoopCount = intLoopCount - 1
	  	WEnd
	
		If intLoopCount <= 0 Then
			ShowMessage fataLvl, "Timed out while zipping folder with large or numerous files. Do it manually."
		End If
    End With

    CompressFolder = zipFile
End Function


Sub SetFormContent
	Const strNotInArchive = "File must be in the Case File Archive first."
	Const strInArchive = "File is already in the Case File Archive."
	
	'Set up top line, document title
	Dim numFileIdx : numFileIdx = UBound(aryFilePaths)
	If numFileIdx > 0 Then
		FileName.InnerHTML = strFileName & " (+ " & numFileIdx & " other files)"
	Else
		FileName.InnerHTML = strFileName
	End If
	
	'Set up "office" radio buttons
	'Why is document.getElementsByClassName() not supported?
	If boolInArch Then
		DisabledInArch.style.color = "gray"
		
		archRadio.title = strInArchive
		CMSNumber.title = strInArchive
	Else
		EnabledInArch.style.color = "gray"
		EnabledInArch2.style.color = "gray"
		EnabledInArch3.style.color = "gray"
		
		'cloudRadio.title = strNotInArchive
		retdRadio.title = strNotInArchive
		holdCheck.title = strNotInArchive'
		Defendant.title = strNotInArchive
	End If	
		
	archRadio.disabled = boolInArch	
	archRadio.checked = Not boolInArch
    
	retdRadio.disabled = Not boolInArch
	retdRadio.checked = boolInArch

    If boolNoDisco Then
		Document.title = "CaseFile_Archive Uploader v2.1"
		' Disable discovery options if specified from command line
		archRadio.disabled = boolNoDisco
		retdRadio.disabled = boolNoDisco
		retdRadio.checked = Not boolNoDisco		
		pdRadio.disabled = boolNoDisco
		adRadio.disabled = boolNoDisco
		acRadio.disabled = boolNoDisco
		mcRadio.disabled = boolNoDisco
		EnabledAsDisco.style.color = "gray"
	End If

	'cloudRadio.disabled = Not (boolInArch And boolDebug)
	    
	holdCheck.disabled = Not boolInArch
    coverCheck.disabled = Not boolInArch

  	'Set up "CMSNumber" text input
	CMSNumber.disabled = boolInArch AND (NOT boolDebug)

	'Set up "Defendant" drop down
	Defendant.disabled = NOT boolInArch

	FileType.disabled = boolInArch
  		
  	'Set up "Send Photograph Summary" button
  	Dim boolCanSummary : boolCanSummary = False
  	If Not boolPics Then
  		summCheck.title = "Select more than one picture to generate Summary."
  		EnabledIfPics.style.color = "gray"
  	Elseif Not (boolPro Or boolGS) Then
  	  	summCheck.title = "PDF Authoring Software not installed."
  	  	EnabledIfPics.style.color = "gray"
  	Else 'boolPics And (boolPro Or boolGS)
  		boolCanSummary = True
  	End If
  	
  	summCheck.disabled = Not boolCanSummary
  	summCheck.checked = boolCanSummary

 	If IsNumeric(Mid(strCMSCase, 2, 5)) Then
		'Split on " " makes uploading 911 calls easier
		CMSNumber.Value = Split(strCMSCase," ")(0)
	End If

	'Set up defendant selection
	' Set up a default, but don't add it yet.
	Set objOption = Document.createElement("OPTION")
	objOption.Value = ".Dis"
	objOption.Text = "Default"

	If boolInArch Then
		Dim objDef, i
		i=1
		'setup the drop down menu
		For Each objDef In getDefendants()
			If objFSO.GetExtensionName(objDef) = "txt" then
				Set objOption = Document.createElement("OPTION")
				objOption.Value = i & "." & objDef.Name
				objOption.Text = objFSO.GetBaseName(objDef)
				Defendant.Add(objOption)
				i = i+1
			End If
		Next
		If i=1 Then
			' No D's added yet. Add default
			Defendant.Add(objOption)
		End If
	Else
		' Not in Archive. Add default
		Defendant.Add(objOption)
	End If
End Sub


Function TypeErrorForFile(strPath)
	TypeErrorForFile = ""

	Select Case LCase(objFSO.GetExtensionName(strPath))
		Case "cda"
			TypeErrorForFile = "Rip this disc with Windows Media Player before uploading."
		Case "dxa"
			'Proprietary surveillance software.
			TypeErrorForFile = "Provide a copy of this CD instead of uploading it."
		Case "exe"
			TypeErrorForFile = "Provide a copy of this CD instead of uploading it."
		Case "lnk"
			TypeErrorForFile = "Open the file location of the shortcut and upload that file instead."
	End Select

	If Len(TypeErrorForFile) > 0 Then
		TypeErrorForFile = 	objFSO.GetFileName(strPath) & " is not a valid data file. " & TypeErrorForFile
	End If
End Function

	
Function ReadRegistry(strRegistryKey)

    On Error Resume Next

	ReadRegistry = Nothing
    ReadRegistry = objShell.RegRead( strRegistryKey )

End Function


Sub ShowMessage(intLvl, strMessage)
	Dim strPrefix : strPrefix = ": "
	Select Case intLvl
		Case debugLvl
			strPrefix = "DEBUG"	& strPrefix
		Case infoLvl
			strPrefix = "INFO"	& strPrefix
		Case warnLvl
			strPrefix = "WARNING"	& strPrefix
		Case errorLvl
			strPrefix = "ERROR"	& strPrefix
		Case fatalLvl
			strPrefix = "FATAL"  & strPrefix
		Case silentLvl
		Case Else
			strPrefix = ""
	End Select

	If intLvl <= logLvl Then
		MsgBox(strPrefix & strMessage)
		'Debug.WriteLine(strPrefix & strMessage)
	End If
	If intLvl = fatalLvl Then
		Self.Close()
	End If
End Sub


Sub ShowHelp
	objShell.Run """" & clientPath & "eDiscovery_EDMS_Uploader.pdf"""
End Sub

</script>

<span style="position:absolute;right:10px;"><a href="#" onclick="ShowHelp()">[Help]</a></span>
<h5>Select the file destination:</h5>
<span id = "DisabledInArch" class="DisabledInArch">
<input type="radio" id="archRadio" name="office" value="Archive" disabled checked> City Attorney CaseFile Archive
</span><br>

<span id = "EnabledAsDisco" class="EnabledAsDisco">
<span id = "EnabledInArch" class="EnabledInArch">
<!--input type="radio" id="cloudRadio" name="office" value="OneDrive" disabled> OneDrive<br-->
<input type="radio" id="retdRadio"  name="office" value="Retained" disabled> Retained Counsel
</span><br><br>
<input type="radio" id="pdRadio" name="office" value="PrimaryDefender"  > Primary Public Defender<br>
<input type="radio" id="adRadio" name="office" value="AlternateDefender"> Alternate Public Defender<br>
<input type="radio" id="acRadio" name="office" value="AssignedCounsel"  > Office of Assigned Counsel<br>
<input type="radio" id="mcRadio" name="office" value="MultipleConflicts"> Multiple Conflicts Counsel<br>
</span>

<h5>Additional Options:</h5>

<span id = "EnabledInArch2" class="EnabledInArch">
	<input type="checkbox" id="coverCheck" name="coversheet" class="EnabledInArch" disabled> Add eDiscovery Cover Sheet &nbsp&nbsp&nbsp
	<input type="checkbox" id="holdCheck" name="placeholder" class="EnabledInArch" disabled> Print Placeholder Sheet
</span><br />
<span id = "EnabledIfPics"  class="EnabledIfPics"><input type="checkbox" id="summCheck" name="pdfsummary"  class="EnabledIfPics" disabled> Send Photograph Summary</span><br><br>

<table>
	<tr>
		<td>CMS Case Number:</td>
		<td><input type="text" name="CMSNumber" disabled></td>
	</tr>
	<tr>
		<td><span id = "EnabledInArch3" class="EnabledInArch">Defendant:</span></td>
		<td>
			<select name="Defendant"></select>
		</td>
	</tr>
	<tr>
		<td>Type of file:</td>
		<td>
 <select name="FileType" disabled>
  <option value="">Default</option>
  <option value="RTS">Agency Reports-Right Side</option>
  <option value="RAP">RAPS</option>
  <option value="LTS">Agency Reports-Left Side</option>
  <option value="CAW">DCA Working File (editable)</option>
  <option value="CRV">CRE (DCA)</option>
  <option value="TRN">Transcripts</option>
  <option value="MOT">Motions</option>

  <option value="CII">Confidential Informant</option>
  <option value="COR">Correspondence</option>
  <option value="VIC">Victim Forms/Receipts</option>
  <option value="COM">Court Filings</option>
  <option value="PAS">Private Atty Share</option>
  <option value="OTH">Other</option>
  <option disabled>&#9472; FOR OLD CASES ONLY! &#9472;</option>
  <option value="CNF">Left Side-Confidential</option>
  <option value="URD">Left Side-Needs Review/Redaction</option>
  <option value="RPS">RAPS</option>
 </select>
		</td>
	</tr>
</table>
 
<br>
<span id = "SubmitButton"><input type="button" value="Submit File" type="submit" onClick="Upload"></span>
<hr>
<span id = "Preview"><input type="button" value="Preview" onClick="EmbedPreview"><br /><span id = "FileName">Loading...</span></span><br>
</body>